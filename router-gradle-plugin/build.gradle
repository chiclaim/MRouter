apply plugin: 'groovy'
if (GRADLE_PLUGIN_UPLOAD_LOCAL.toBoolean()) {
    apply plugin: "maven"
}

ext {
    bintrayName = 'mrouter-gradle-plugin'
    artifact = bintrayName
    libraryDescription = 'Android Modularization Gradle Plugin'
    libraryVersion = MROUTER_GRADLE_PLUGIN_VERSION
}

dependencies {

    compile gradleApi()
    compile localGroovy()

    compileOnly "com.android.tools.build:gradle:${GRADLE_PLUGIN_VERSION}"
    compile "org.ow2.asm:asm:${ASM_VERSION}"
    compile "org.ow2.asm:asm-commons:${ASM_VERSION}"

}

compileGroovy {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    options.encoding = "UTF-8"
}

repositories {
    jcenter()
    maven {
        url '../maven'
    }
}

// 解决运行 uploadArchive 提示类中不能有中文的问题
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

if (GRADLE_PLUGIN_UPLOAD_LOCAL.toBoolean()) {
    // 上传到本地代码库，方便测试
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: uri('../maven'))
                pom.groupId = PUBLISHED_GROUP_ID // 组名
                pom.artifactId = artifact // 插件名
                pom.version = libraryVersion // 版本号
            }
        }
    }
} else {
    apply from: '../maven.gradle'
    apply from: '../bintray.gradle'
}